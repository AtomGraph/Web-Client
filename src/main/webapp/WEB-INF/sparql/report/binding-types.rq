PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX vis: <http://code.google.com/apis/visualization/>
PREFIX rep: <http://www.semantic-web.dk/ontologies/semantic-reports/>

SELECT DISTINCT ?type ?label ?visType ?cardinality ?minCardinality ?maxCardinality ?order
{
	_:super rdfs:subClassOf vis:Binding .
	?type   rdfs:subClassOf _:super .
        ?type   rdfs:label      ?label .
        _:prop  rdfs:range      ?type .
        _:prop  rdfs:domain     ?visType
        # cardinalities
        OPTIONAL
        {
            ?visType    rdfs:subClassOf ?rest .
            ?rest       owl:onProperty  vis:binding .
            ?rest       owl:onClass     ?type
            OPTIONAL
            {
                ?rest   owl:qualifiedCardinality    ?cardinality
            }
            OPTIONAL
            {
                ?rest   owl:minQualifiedCardinality ?minCardinality
            }
            OPTIONAL
            {
                ?rest   owl:maxQualifiedCardinality ?maxCardinality
            }
        }
        # order
        OPTIONAL
        {
            ?type   rdfs:subClassOf ?x .
            ?x   owl:onProperty  vis:order .
            ?x   owl:hasValue    ?order
        }
}
ORDER BY ?label